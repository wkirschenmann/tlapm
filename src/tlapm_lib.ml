(* Driver.

Copyright (C) 2008-2010  INRIA and Microsoft Corporation
*)
open Ext
open Property
open Util.Coll


module P = Tla_parser.P
module Module = Module
module Property = Property
module Proof = Proof
module Expr = Expr
module Util = Util
module Loc = Loc
module Ctx = Ctx
module Backend = Backend
module Builtin = Builtin

open Proof.T

open Module.T

module Clocks = struct
  include Timing
  (* The named clocks (parsing, elab, gen, ...) are defined in Timing so
     that subsystem code can attribute time to them; [include] re-exports
     them here. *)


  let pad_left md str = Printf.sprintf "%*s" md str

  let report () =
    let clocks =
      [ total "total"; parsing; elab; gen; prep; print; backend; check;
        fp_loading; fp_saving; fp_compute; ambient ]
    in
    let max_desc_width =
      List.fold_left (fun mx cl -> max mx (String.length cl.desc)) 0 clocks
    in
    let clocks =
      List.map (fun cl -> {cl with desc = pad_left max_desc_width cl.desc})
               clocks
    in
    Util.printf "(* %s | time (seconds) *)"
                (pad_left max_desc_width "operation");
    Util.printf "(* %s-+--------------- *)" (String.make max_desc_width '-');
    List.iter begin
      fun cl -> Util.printf "(* %s  *)%!" (string_of_clock cl)
    end (List.tl clocks);
    Util.printf "(* %s-+--------------- *)" (String.make max_desc_width '-');
    Util.printf "(* %s  *)" (string_of_clock (List.hd clocks));

end


let mkdir_tlaps t =
    let cachedir = !Params.cachedir in
    let tlapsdir = cachedir ^ "/" ^ t.core.name.core ^ ".tlaps" in
    if not (Sys.file_exists cachedir) then begin
        try
            Unix.mkdir cachedir 0o777
        with error ->
            let error_msg = Printexc.to_string error in
            Errors.err "Could not create the \
                cache directory at the path \
                `%s`. Please ensure that if \
                a path is given via the \
                command-line parameter \
                `--cache-dir`, or via the \
                variable `TLAPM_CACHE_DIR` of \
                the runtime environment, \
                that a directory at that path \
                can be created. \n\
                The error message from calling \
                `Unix.mkdir` is:\n%s"
                    cachedir error_msg
    end;
    if not (Sys.file_exists tlapsdir) then Unix.mkdir tlapsdir 0o777;
    (cachedir, tlapsdir)


let handle_abort _ =
  if !Params.verbose then
    Util.eprintf ~prefix:"FATAL: " ">>> Interrupted -- aborting <<<" ;
  if !Params.stats then
    Clocks.report () ;
  if Backend.Interrupted.mark_interrupted () then
    (* Keep going on the first SigINT to shutdown
       gracefully. Exit on the repeated signal. *)
    Stdlib.exit 255


module IntSet = Set.Make (struct type t = int let compare = (-) end)

let modules_list : string list ref = ref []


let print_proved_obligation (ob: Proof.T.obligation) =
    if !Params.verbose then
        Util.printf
            ~at:ob.Proof.T.obl
            ~prefix:"[INFO]: "
            "@[<v2>Attempted to prove or check:@,%a@]@."
            Proof.Fmt.pp_print_obligation ob


let print_proved_obligations
        (proved: Proof.T.obligation list ref)
        (obs: Proof.T.obligation array)
        (tla_module: Module.T.mule): unit =
    List.iter print_proved_obligation !proved


let print_unproved_obligation (ob: Proof.T.obligation) =
    Util.eprintf
        ~at:ob.Proof.T.obl
        ~prefix:"[ERROR]: "
        "@[<v2>Could not prove or check:@,%a@]@."
        Proof.Fmt.pp_print_obligation ob


let print_unproved_obligations
        (unproved: Proof.T.obligation list ref)
        (obs: Proof.T.obligation array)
        (tla_module: Module.T.mule): unit =
    List.iter print_unproved_obligation !unproved;
    let n_unproved = List.length !unproved in
    let n_obligations = Array.length obs in
    let s = if n_obligations > 1 then "s" else "" in
    let n_neg = string_of_int n_unproved in
    let n_all = string_of_int n_obligations in
    if n_unproved <> 0 then begin
        Util.eprintf
            ~at:tla_module "%s"
            ~prefix:"[ERROR]: "
            (n_neg ^ "/" ^ n_all ^ " obligation" ^ s ^ " failed.");
        Util.eprintf
            "There were backend errors processing module `%S`."
            tla_module.core.name.core;
        if !Params.strict then
            (* Record the most severe condition and keep processing so that all
               modules are reported before exiting (see [Params.exit_status]). *)
            Params.note_strict_failure 10
        else if not !Params.toolbox then
            failwith "backend errors: there are unproved obligations"
    end else begin
        Util.eprintf
            ~at:tla_module "%s"
            ~prefix:"[INFO]: "
            ("All " ^ n_all ^ " obligation" ^ s ^ " proved.")
    end


let toolbox_consider ob =
    let loc = Option.get (Util.query_locus ob.Proof.T.obl) in
    !Params.tb_sl <= Loc.line loc.Loc.start
    && Loc.line loc.Loc.stop <= !Params.tb_el


(* Note that when process_obs is called, all obligations have id fields
   (eager mode; in lazy mode, ?stepper, ids are assigned at emission in
   the same document order). *)
let process_obs
        ?stepper
        (t: Module.T.mule)
        (obs: Proof.T.obligation array)
        (modname: string)
        (thyf: string)
        (fpf: string):
            unit =
    (* initialize table of treated obligations *)
    let treated = ref IntSet.empty in
    (* initialize table of proved obligations *)
    let proved_ids = ref IntSet.empty in
    (* Lazy generation (?stepper): obligations are pulled from the
       generation stepper as the scheduler demands them instead of from
       the pre-materialized [obs] array (empty in that mode).  Every
       dispatched obligation is retained in [seen] for the final
       report; its sequent is dropped as soon as a success verdict is
       recorded — only unproved obligations are ever reprinted. *)
    let seen : (int, Proof.T.obligation) Hashtbl.t = Hashtbl.create 64 in
    (* The array the final report iterates: [obs] in eager mode,
       rebuilt from [seen] after the drain in lazy mode. *)
    let report_obs = ref obs in
    (* Measurement probe: with TLAPM_LIVE_STATS=N set, print the live and
       total heap sizes every N recorded verdicts. This is the instrument
       that attributes the per-obligation live-memory floor observed on
       large single-pass runs (Gc.stat walks the heap, so use a large N).
       Inert when the variable is unset. *)
    let live_every =
        match Sys.getenv_opt "TLAPM_LIVE_STATS" with
        | Some s -> (try int_of_string s with Failure _ -> 0)
        | None -> 0 in
    let recorded = ref 0 in
    let record
            (success: bool)
            (ob: Proof.T.obligation):
                unit =
        (* the number `obl_id \in Nat` that identifies the obligation `ob` *)
        let obl_id = Option.get ob.id in
        (* store the obligation id number in the internal table `treated` *)
        treated := IntSet.add obl_id !treated;
        (* if the prover (frontend or backend) succeeded, *)
        if success then begin (* then store the obligation id number in
                        the internal table `proved_ids`
                        *)
            proved_ids := IntSet.add obl_id !proved_ids;
            (* lazy mode: the sequent of a proved obligation is not
               needed anymore — keep only the wrapper (its properties
               carry the location the report prints) *)
            match Hashtbl.find_opt seen obl_id with
            | Some sob ->
                let dummy_seq = {
                    Expr.T.context = Deque.empty ;
                    Expr.T.active = {core = Expr.T.String ""; props = []} } in
                Hashtbl.replace seen obl_id
                    { sob with obl = { sob.obl with core = dummy_seq } }
            | None -> ()
        end;
        incr recorded;
        if live_every > 0 && !recorded mod live_every = 0 then begin
            let st = Gc.stat () in
            Printf.eprintf "[LIVE] verdicts=%d live_mb=%.1f heap_mb=%.1f\n%!"
                !recorded
                (float_of_int st.Gc.live_words *. 8. /. 1048576.)
                (float_of_int st.Gc.heap_words *. 8. /. 1048576.)
        end
        in
    let collect_untreated_obligations
            (untreated: Proof.T.obligation list ref):
                unit =
        (* Store in the `list ref` `untreated` all the obligations that
        are not in the `IntSet` `treated`.
        *)
        let f ob =
            let obl_id = Option.get ob.id in
            if not (IntSet.mem obl_id !treated) then
                untreated := ob :: !untreated;
            in
        Array.iter f !report_obs
        in
    let collect_proved_obligations
            (proved: Proof.T.obligation list ref):
                unit =
        let f ob = begin
            let obl_id = Option.get ob.id in
            if (IntSet.mem obl_id !proved_ids) then
                proved := ob :: !proved
            end
            in
        Array.iter f !report_obs
        in
    let collect_unproved_obligations
            (unproved: Proof.T.obligation list ref):
                unit =
        let f ob = begin
            let obl_id = Option.get ob.id in
            if not (IntSet.mem obl_id !proved_ids) then
                unproved := ob :: !unproved
            end
            in
        Array.iter f !report_obs
        in
    Clocks.start Clocks.backend ;
    (* initialize file for output of fingerprints
    (saves fingerprints file history)
    *)
    let fpout = Fpfile.fp_init fpf !modules_list in
    let thyout = Isabelle.thy_init modname thyf in
    let _ = Errors.get_warnings () in
    (* Prepare and prove: tasks are built on demand, in document order
       (the cross-obligation preparation caches assume the sequence),
       so a task's closures — and the prepared obligation forms they
       hold once forced — only live while the task is in flight.
       Materializing the whole task array up front paid every task's
       eager preparation before the first prover started and kept every
       consumed closure reachable for the entire run.
       A task-construction error (Exit) stops the stream: obligations
       already dispatched keep their results, the rest are reported
       untreated (the eager code aborted the whole batch instead). *)
    let make_task = Prep.make_task fpout thyout record in
    let next_i = ref 0 in
    let aborted = ref false in
    (* Lazy generation: the emission pump.  Ids are assigned to every
       obligation the stepper yields, in document order — the same
       numbering the eager path produces with [add_id] before its
       filters — and the eager path's filters (toolbox range, omitted)
       are applied per emission.  Kept obligations get their
       "to be proved" toolbox message here, where the eager path
       printed them all before proving. *)
    let emitted = ref 0 in
    let pending : Proof.T.obligation Queue.t = Queue.create () in
    let stepper_done = ref false in
    let rec refill st =
        if Queue.is_empty pending && not !stepper_done then
            match Module.Gen.gen_step st with
            | None -> stepper_done := true
            | Some new_obs ->
                List.iter begin fun ob ->
                    incr emitted ;
                    let ob = { ob with id = Some !emitted } in
                    let keep =
                        (not !Params.toolbox || toolbox_consider ob)
                        && (match ob.kind with
                            | Ob_omitted _ -> false
                            | _ -> true) in
                    if keep then begin
                        if !Params.toolbox then
                            Backend.Toolbox.toolbox_print ob
                                "to be proved" None None 0. None
                                !Params.printallobs None "" None ;
                        Hashtbl.replace seen !emitted ob ;
                        Queue.add ob pending
                    end
                end new_obs ;
                refill st
    in
    let next_task () =
        if !aborted then None
        else match stepper with
        | None ->
            if !next_i >= Array.length obs then None
            else begin
                try
                    let t = make_task obs.(!next_i) in
                    incr next_i;
                    Some t
                with Exit -> aborted := true; None
            end
        | Some st ->
            refill st ;
            begin match Queue.take_opt pending with
            | None -> None
            | Some ob ->
                (try Some (make_task ob)
                 with Exit -> aborted := true; None)
            end
    in
    (* proving *)
    Schedule.run_stream !Params.max_threads next_task;
    (* Lazy generation: finish the traversal (an abort or a stop can
       leave it partial) so the final report sees every obligation the
       run should have considered, patch the module's summary — which
       eager generation had produced before proving — and rebuild the
       report array from the retained records, in id order. *)
    (match stepper with
     | None -> ()
     | Some st ->
         while not !stepper_done do
             refill st ;
             Queue.clear pending
         done ;
         let summ = Module.Gen.gen_summary st in
         (match t.core.stage with
          | Final fin ->
              t.core.stage <-
                  Final { fin with final_status = (Incomplete, summ) }
          | _ -> ()) ;
         let l = Hashtbl.fold (fun _ ob acc -> ob :: acc) seen [] in
         let l = List.sort ~cmp:(fun a b -> compare a.id b.id) l in
         report_obs := Array.of_list l) ;
    Isabelle.thy_close thyf thyout;
    (* close fingerprints file *)
    Fpfile.fp_close_and_consolidate fpf fpout;
    Clocks.stop ();
    (* `--noproving` command line option or TLA+ Toolbox has sent "stop" *)
    if not (!Params.noproving || (Toolbox.is_stopped ()) || (Backend.Interrupted.is_interrupted ()) ) then begin
    (* Check proof results for each obligation, output summary *)
    if !Params.toolbox then begin
        let untreated = ref [] in
        collect_untreated_obligations untreated;
        let result =
            let c = Types.Cantwork "unexpected error" in
            let fail = Types.RFail (Some c) in
            Types.NTriv (fail, Method.Fail)
            in
        let f ob =
            Toolbox.print_new_res ob result "" None
            in
        List.iter f !untreated
        end;
    (* Printing of proved and unproved obligations *)
    let proved = ref [] in
    let unproved = ref [] in
    collect_proved_obligations proved;
    collect_unproved_obligations unproved;
    print_proved_obligations proved !report_obs t;
    print_unproved_obligations unproved !report_obs t
    end


let add_id i ob = {ob with id = Some (i+1)}



let toolbox_clean arr =
    if !Params.toolbox
        then List.filter toolbox_consider (Array.to_list arr)
        else Array.to_list arr


let process_module
        mcx
        (t: Module.T.mule) =
    modules_list := t.core.name.core :: !modules_list;
    if t.core.important then begin
        let (_, tlapsdir) = mkdir_tlaps t in
        Params.output_dir := tlapsdir;
    end;
    (* names of theory and fingerprint files *)
    (* Theory file name *)
    let thyf: string = Filename.concat
                            !Params.output_dir (t.core.name.core ^ ".thy")
    (* Fingerprints output file name *)
    and fpf: string = Filename.concat
                            !Params.output_dir "fingerprints"
    in
    (* file for output of fingerprints *)
    (Params.fpf_out: string option ref) := Some fpf;
    (* Lazy-generation stepper, set while normalizing (TLAPM_STREAM_GEN). *)
    let stepper = ref None in
    (* cases of module (stage: Module.T.stage) =
        | Special
        | Final _
        | Parsed | Flat
    *)
    let (mcx, t) = match t.core.stage with
    | Special -> (mcx, t)
    | Final _ ->
        if Params.debugging "rep" && t.core.important then begin
            Clocks.start Clocks.print ;
            Module.Fmt.pp_print_module
                (Deque.empty, Ctx.dot) Format.std_formatter t ;
            Format.printf "\n%!" ;
            Clocks.stop () ;
        end ;
        (mcx, t)
    | _ ->
        if !Params.verbose then begin
            Util.printf "(* processing module %S *)" t.core.name.core
        end ;
        Clocks.start Clocks.elab ;
        (* Lazy generation (TLAPM_STREAM_GEN=1): obligations are pulled
           from a resumable generation stepper as the scheduler demands
           them, instead of being materialized in final_obs before any
           proving starts.  Restricted to plain proving runs: the modes
           below consume the eager artifacts (per-theorem summaries in
           the rewritten body, the module summary before proving, the
           full obligation array), so they keep the historical path. *)
        let stream_gen =
            Sys.getenv_opt "TLAPM_STREAM_GEN" <> None
            && not !Params.summary
            && not !Params.check
            && not !Params.stats
            && not !Params.suppress_all
            && not (Params.has_explicit_target ())
        in
        (* Normalize the proofs in order to get proof obligations *)
        let (mcx, t, summ) =
            if stream_gen then
                Module.Elab.normalize
                    ~stream:(fun st -> stepper := Some st)
                    mcx Deque.empty t
            else
                Module.Elab.normalize mcx Deque.empty t
        in
        (* In stream mode the summary is empty at this point; the
           obligation count, where needed below, comes from the
           syntactic pre-pass instead. *)
        let counted =
            lazy (fst (Module.Gen.count_obligations_split t)) in
        let has_obligations () =
            match !stepper with
            | Some _ -> Lazy.force counted <> 0
            | None -> summ.sum_total <> 0
        in
        (*
        List.iter
            (fun u -> Module.Fmt.pp_print_module
                (Deque.empty, Ctx.dot) Format.std_formatter (snd u))
            (Sm.bindings mcx);
        *)
        (*
        let _ = Sm.iter
                    (fun f u -> Printf.printf "Module: %s\n" u.core.name.core)
                    mcx in
        *)
        Clocks.stop () ;
        if Params.debugging "rep" && t.core.important then begin
            Clocks.start Clocks.print ;
            Module.Fmt.pp_print_module
                (Deque.empty, Ctx.dot)
                Format.std_formatter t ;
            Format.printf "\n%!" ;
            Clocks.stop () ;
        end ;
        if !Params.stats && t.core.important then begin
            Util.printf
                "(* module %S: %d total %s *)"
                t.core.name.core
                summ.sum_total
                (Util.plural summ.sum_total "obligation")
        end ;

        (* managing the fingerprints (erasing or loading) *)
        (* `--cleanfp` command line option *)
        if !Params.cleanfp && t.core.important then begin
            try
                Sys.remove fpf;
                Util.printf "(* fingerprints file %S removed *)%!" fpf
            with _ ->
                if Sys.file_exists fpf then
                    Util.printf "%s%s" (
                        "(* did not succeed in removing " ^
                        "fingerprints file %S *)%!") fpf
        end
        else begin
            (* file for input of fingerprints *)
            let fpf_in = begin
                match !Params.fpf_in with
                    | Some f -> f  (* `--usefp` command line option *)
                    | None -> fpf  (* same as file for output of fingerprints *)
            end in
            if      Sys.file_exists fpf_in
                    && has_obligations ()
                    && t.core.important
                then begin
                if !Params.no_fp then
                    Util.printf "(* will not use fingerprints \
                        (because of option `--nofp`), \
                        but will now load fingerprints from \
                        the file `%s`, in order to overwrite with \
                        the new fingerprints, and then save \
                        the results at the end. *)%!"
                        fpf_in
                else
                    Util.printf
                        "(* loading fingerprints in %S *)%!"
                        fpf_in;
                Clocks.start Clocks.fp_loading;
                (* load fingerprints from input file *)
                Backend.Fpfile.load_fingerprints fpf_in;
                Params.fp_loaded := true;
                Params.fp_original_number := Backend.Fpfile.get_length ();
                Clocks.stop ()
             end
        end;

        flush stdout ;
        Clocks.start Clocks.prep ;
        let fin = match t.core.stage with
            | Final fin -> fin
            | _ -> Errors.bug ~at:t
                        "normalization didn't produce a finalized module"
        in
        let obs = Array.mapi add_id fin.final_obs in  (* add obligation ids *)
        let obs = toolbox_clean obs in (* only consider specified obligations *)
        let obs = List.filter (fun ob -> match ob.kind with Ob_omitted _ -> false | _ -> true) obs in
        let fin = {
                fin with final_obs = Array.of_list obs ;
                final_status = (Incomplete, summ) } in
        t.core.stage <- Final fin ;
        (* Measurement probe: with TLAPM_TRACE_DEFS set, print one line per
           processed module tallying the hypotheses of every obligation
           context — count, total/max size, and the Defn breakdown by
           visibility and export — the quantities that drive backend
           preparation cost. Set it to a comma-separated list of name
           fragments (e.g. instance prefixes such as "Foo!") to also tally
           how many context definitions each fragment accounts for. Inert
           when the variable is unset. *)
        (match Sys.getenv_opt "TLAPM_TRACE_DEFS" with
         | None -> ()
         | Some arg ->
            let open Expr.T in
            let n_obl = Array.length fin.final_obs in
            let t_defn = ref 0 and t_vis_op = ref 0 and t_hid_op = ref 0
            and t_bpragma = ref 0 and t_local = ref 0 and t_export = ref 0
            and t_ctx = ref 0 and max_ctx = ref 0 in
            let fragments =
              List.filter (fun s -> s <> "" && s <> "1")
                (String.split_on_char ',' arg) in
            let by_frag = List.map (fun p -> (p, ref 0)) fragments in
            let contains hay needle =
              let nh = String.length needle and lh = String.length hay in
              let rec go i =
                i + nh <= lh
                && (String.sub hay i nh = needle || go (i + 1)) in
              go 0 in
            let bump_frag nm =
              List.iter (fun (p, r) -> if contains nm p then incr r) by_frag
            in
            Array.iter (fun ob ->
              let ctx = ob.Proof.T.obl.core.context in
              let this_ctx = Deque.size ctx in
              t_ctx := !t_ctx + this_ctx ;
              if this_ctx > !max_ctx then max_ctx := this_ctx ;
              Deque.iter (fun _ h ->
                match h.core with
                | Defn (df, _wd, vis, ex) ->
                    incr t_defn ;
                    (match ex with
                     | Local -> incr t_local
                     | Export -> incr t_export) ;
                    (match df.core, vis with
                     | Operator (nm, _), Visible ->
                        incr t_vis_op ; bump_frag nm.core
                     | Operator (nm, _), Hidden ->
                        incr t_hid_op ; bump_frag nm.core
                     | Bpragma (nm, _, _), _ ->
                        incr t_bpragma ; bump_frag nm.core
                     | _ -> ())
                | _ -> ()
              ) ctx
            ) fin.final_obs ;
            Printf.eprintf
              "[TRACE_DEFS] module=%s important=%b obligations=%d \
               total_ctx_hyps=%d max_ctx_hyps=%d Defn=%d \
               (Visible_op=%d Hidden_op=%d Bpragma=%d) \
               export[Local=%d Export=%d] expandable~=%d\n%!"
              t.core.name.core t.core.important n_obl
              !t_ctx !max_ctx !t_defn
              !t_vis_op !t_hid_op !t_bpragma
              !t_local !t_export (!t_vis_op + !t_bpragma) ;
            if by_frag <> [] then
              Printf.eprintf
                "[TRACE_DEFS]   by-fragment (ctx-def occurrences): %s\n%!"
                (String.concat "  "
                   (List.map (fun (p, r) -> Printf.sprintf "%s=%d" p !r)
                      by_frag))) ;
        Module.Save.store_module ~clock:Clocks.elab t ;
        Clocks.stop () ;  (* close the [prep] clock started above *)
        (mcx, t)
    in
    if !Params.summary && t.core.important then
        Module.Fmt.summary t ;
    begin match t.core.stage with
    | Final fin when t.core.important
        (* Array.length fin.final_obs > 0 *)
        ->
        if !Params.verbose then
            Util.printf "(* module %S: %d unique obligations *)"
                t.core.name.core
                (Array.length fin.final_obs) ;

        if (!Params.toolbox) then begin
            match !stepper with
            | Some _ ->
                (* Lazy generation: the per-obligation "to be proved"
                   messages are printed at emission (see process_obs);
                   the total comes from the syntactic pre-pass, minus
                   the omitted obligations the eager path filters out
                   of its announced count. *)
                let (total, omitted) =
                    Module.Gen.count_obligations_split t in
                Backend.Toolbox.print_ob_number (total - omitted)
            | None ->
            let f ob =
                Backend.Toolbox.toolbox_print
                    ob
                    "to be proved"
                    None
                    None
                    0.
                    None
                    !Params.printallobs None "" None
            in
            (* prints the list of obligations which have to be proved *)
            Array.iter f fin.final_obs;
            (* prints total number of obligations *)
            Backend.Toolbox.print_ob_number
                (Array.length fin.final_obs) ;
        end;

        let missing =
            match fin.final_status with
            | (Incomplete, miss) -> snd (miss.sum_absent)
            | _ -> []
        in

        (* `--strict` checks that are independent of the backends. *)
        let strict_checks (fin: Module.T.final) =
            (* #271: missing or omitted proof steps leave the proof incomplete,
               even though such steps generate no obligation and would otherwise
               be reported as a successful run. *)
            let (_, summ) = fin.final_status in
            let (n_absent, absent) = summ.sum_absent in
            let (n_omitted, omitted) = summ.sum_omitted in
            (* `--summary` already lists each missing/omitted proof location, so
               skip the per-locus lines to avoid duplicate output. *)
            if not !Params.summary then begin
                List.iter
                    (fun loc -> Util.eprintf ~prefix:"[ERROR]: "
                        "Missing proof at %s" (Loc.string_of_locus loc))
                    absent;
                List.iter
                    (fun loc -> Util.eprintf ~prefix:"[ERROR]: "
                        "Omitted proof at %s" (Loc.string_of_locus loc))
                    omitted
            end;
            (* Emit one module-level summary line and raise the exit status to
               11 (incomplete proof) whenever any step is missing or omitted. *)
            if n_absent + n_omitted > 0 then begin
                Util.eprintf ~at:t ~prefix:"[ERROR]: "
                    "Proof incomplete in module %S: %d missing, %d omitted \
                     proof step(s)."
                    t.core.name.core n_absent n_omitted;
                Params.note_strict_failure 11
            end;
            (* #276: an explicit target (e.g. `--line`) that selects no
               obligation usually means an off-by-one line number rather than a
               genuine proof. A whole-module run with zero obligations is fine. *)
            if Params.has_explicit_target ()
               && Array.length fin.final_obs = 0 then begin
                Util.eprintf ~at:t ~prefix:"[ERROR]: "
                    "No proof obligation found for the selected target.";
                Params.note_strict_failure 12
            end
        in
        (* Lazy generation: the summary the checks read is only complete
           once the stepper has drained, so they run after process_obs
           (same messages and exit codes, printed after the verdicts
           instead of before). *)
        (match !stepper with
         | None when !Params.strict -> strict_checks fin
         | _ -> ());

        begin
        if not !Params.suppress_all then
            process_obs ?stepper:!stepper t fin.final_obs
            t.core.name.core thyf fpf;
        (match !stepper with
         | Some _ when !Params.strict ->
             (match t.core.stage with
              | Final fin -> strict_checks fin
              | _ -> ())
         | _ -> ());

        (** Added by HV. It collects all facts and definitions used in the
        current proof tree into a one-line proof. *)
        if Params.debugging "oneline" && t.core.important then begin
            Util.eprintf "One-line proof:\n";
            match Module.Gen.collect_usables t with
                | None -> Util.eprintf "  OBVIOUS"
                | Some u ->
                    let s = Util.sprintf "@[<hov2>%a@]"
                        (Proof.Fmt.pp_print_usable (Deque.empty,Ctx.dot)) u in
                    let s = Str.global_replace (Str.regexp "?") "" s in
                        Util.eprintf "  BY @[<hov2>%s@]@.\n" s
        end else ();

        let wrap x = { core = x; props = [] } in
        let dummy_seq = {
            Expr.T.context = Deque.empty;
            Expr.T.active = wrap (Expr.T.String "");
            } in
        let dummy_ob: Proof.T.obligation = {
                id = None;
                obl = wrap dummy_seq;
                fingerprint = None;
                kind = Ob_main;
            } in
        Array.fill fin.final_obs 0 (Array.length fin.final_obs) dummy_ob;

        if t.core.important && !Params.check
           && not (Toolbox.is_stopped ()
                   || Backend.Interrupted.is_interrupted ()) then begin
            Clocks.start Clocks.check ;
            let modname = t.core.name.core in
            let nmiss = List.length missing in
            Std.finally Clocks.stop Isabelle.recheck (modname, nmiss, thyf)
        end;
        (mcx, t)
        end
    | _ -> (mcx, t)
    end

let read_new_modules mcx fs =
  (* Read the files with names in the list of strings `fs`. *)
  List.fold_left begin
    fun (mcx, mods) fn ->
      let hint = Util.locate fn Loc.unknown in
      (* The flag `Params.use_stdin` can only be set to true if only a single
         file is passed to the `tlapm`. Therefore, the following assignment of
         the `use_stdin_prop` property will only be applied to a single file.
         This way, the file passed explicitly will be read from `stdin`, and all
         the files referenced from it will be searched in a file system, as usual. *)
      let hint = match !Params.use_stdin with
      | true -> Property.assign hint Module.Save.module_content_prop (Module.Save.Channel Stdlib.stdin)
      | false -> hint
      in
      let mule =
        Module.Save.parse_file ~clock:Clocks.parsing hint
      in
      let mule = Intermediate.expand mule in
      (* set a flag for each module of the new modules that it is important *)
      mule.core.important <- true ;
      let mcx = Sm.add mule.core.name.core mule mcx in
      (* `mcx` is a mapping from names of already loaded modules and
      modules loaded by the function `read_new_modules`, to modules
      (values of type `mule`)

      `mods` is a list of module (values of type `mule`) that are loaded by
      the function `read_new_modules`.
      *)
      (mcx, mule :: mods)
  end (mcx, []) fs


let print_modules mcx =
    print_string "\nModules loaded so far:\n";
    Sm.iter (fun name mule -> Printf.printf "Module name: \"%s\"\n" name) mcx

let append_ext_if_not_tla filename =
    if Filename.check_suffix filename ".tla" then
        filename
    else
        filename ^ ".tla"


let map_paths_to_filenames paths =
    let basenames = List.map Filename.basename paths in
    List.map append_ext_if_not_tla basenames

let setup_loader fs loader_paths =
  let add_if_new acc f =
    let base_dir = Filename.dirname f in
    match List.mem base_dir acc with
    | true -> acc
    | false -> base_dir :: acc
  in
  let loader_paths = Filename.current_dir_name :: loader_paths in
  let loader_paths = List.fold_left add_if_new loader_paths fs in
  Loader.Global.setup loader_paths

let main fs =
  setup_loader fs !Params.rev_search_path;
  Params.input_files := map_paths_to_filenames fs;
  let () =
    List.iter begin
      fun s ->
        ignore (Sys.signal s (Sys.Signal_handle handle_abort))
    end [Sys.sigint ; Sys.sigabrt ; Sys.sigterm] in
  let () = Format.pp_set_max_indent Format.std_formatter 2_000_000 in
  let mcx = Module.Standard.initctx in
  (* reads the new modules and return both the map:name->module with the new
   * module and a list of the new modules*)
  (* The module names (or file names) in the list `fs` are those listed on
  the command line. Each file is read by the function `read_new_modules .
  Submodules are read below.
  *)
  let (mcx, mods) = read_new_modules mcx fs in
  (* print_modules mcx; *)
  if List.length mods = 0 then begin
    Util.eprintf ~prefix:"FATAL: " "could not read any modules successfully!" ;
    failwith "fatal error: could not read any modules";
  end else begin
    (* load the transitive closure over extends of all modules *)
    (* TODO: load also modules that occur in `INSTANCE` statements from extended modules. *)
    let mcx = Module.Save.complete_load ~clock:Clocks.parsing mcx in
    (* flatten the modules *)
    let (mcx, mods) = Module.Dep.schedule mcx in
      let f mcx m =
        (* processing the proofs in the commandline modules *)
        let (mcx, m) = process_module mcx m in
        Sm.add m.core.name.core m mcx
      in
      ignore (List.fold_left f mcx mods)
  end ;
  if !Params.stats then Clocks.report () ;
  (* Under `--strict`, exit with the most severe condition encountered. A clean
     run leaves [exit_status] at 0 and falls through to the normal exit. The
     `--strict` guard keeps non-strict runs unaffected even if [exit_status] is
     ever set elsewhere or retained across in-process invocations. *)
  if !Params.strict && !Params.exit_status <> 0 then exit !Params.exit_status


let init () =
  Random.self_init();
  Printexc.record_backtrace true;
  Format.pp_set_max_indent Format.err_formatter 35;
  if Params.debugging "main" then
    main (Tlapm_args.init Sys.executable_name Sys.argv)
  else
    try main (Tlapm_args.init Sys.executable_name Sys.argv) with
    | Errors.Fatal ->
       Util.eprintf "tlapm: Exiting because of the above error.";
       exit 0;
    | e ->
       let backtrace = Printexc.get_backtrace () in
       Format.pp_print_flush Format.std_formatter ();
       Format.pp_print_flush Format.err_formatter ();
       Stdlib.flush stdout;
       Stdlib.flush stderr;
       let error = (Printexc.to_string e) ^ "\n" ^ backtrace in
       Util.eprintf ~prefix:"FATAL:" " tlapm ending abnormally with %s" error;
       let config = Params.print_config_toolbox false in
       begin match !Errors.loc, !Errors.msg with
       | Some l, Some m -> Backend.Toolbox.print_message (l ^  "\n\n" ^ m)
       | None, Some m -> Backend.Toolbox.print_message m
       | _, _ ->
          let msg =
            Printf.sprintf
               "Oops, this seems to be a bug in TLAPM.\n\
                Please give feedback to developers.\n\n\n %s\n%s"
               error config
          in
          let url = "http://tla.msr-inria.inria.fr/bugs" in
          Backend.Toolbox.print_message_url msg url;
       end;
       exit 3

(* Access to this function has to be synchronized. *)
let modctx_of_string ~(content : string) ~(filename : string) ~loader_paths ~prefer_stdlib : (modctx * Module.T.mule, string option * string) result =
    let parse_it () =
        Errors.reset ();
        Params.prefer_stdlib := prefer_stdlib;
        setup_loader [filename] loader_paths;
        let hint = Util.locate filename Loc.unknown in
        let hint = Property.assign hint Module.Save.module_content_prop (Module.Save.String content) in
        let mule = Module.Save.parse_file ~clock:Clocks.parsing hint in
        Params.input_files := [Filename.basename filename]; (* Needed, because p_gen.ml decides if obs should be generated by this. *)
        Params.set_search_path [Filename.dirname filename]; (* Were to look for referenced files. TODO: Take additional. *)
        mule.core.important <- true ;
        let mcx = Module.Standard.initctx in
        let mcx = Sm.add mule.core.name.core mule mcx in
        let mcx = Module.Save.complete_load ~clock:Clocks.parsing mcx in
        let (mcx, mods) = Module.Dep.schedule mcx in
        Clocks.start Clocks.elab ;
        let mcx, mule = Std.finally Clocks.stop (List.fold_left (fun (mcx, found) m ->
            let (mcx, m, _summ) = Module.Elab.normalize mcx Deque.empty m in
            match m.core.name.core = mule.core.name.core with
            | true -> (mcx, Some m)
            | false -> (mcx, found)
        ) (mcx, None)) mods in
        match mule with
        | Some mule -> Ok (mcx, mule)
        | None -> failwith "modctx_of_string, found no module we tried to parse."
    in
    match parse_it () with
    | Ok (mcx, mule) -> Ok (mcx, mule)
    | Error e -> Error e
    | exception e ->
        (match !Errors.loc, !Errors.msg with
         | Some l, Some m -> Error (Some l, m)
         | None, Some m -> Error (None, m)
         | Some l, None -> Error (Some l, Printexc.to_string e)
         | None, None -> Error (None, Printexc.to_string e))

let module_of_string module_str =
    let hparse = Tla_parser.P.use Module.Parser.parse in
    let (flex, _) = Alexer.lex_string module_str in
    Tla_parser.P.run hparse ~init:Tla_parser.init ~source:flex

let stdlib_search_paths = Params.stdlib_search_paths
